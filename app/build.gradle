apply plugin: 'com.android.application'
apply plugin: 'android'
apply plugin: 'newlens'

android {

    // 配置编译信息
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    // 配置版本信息
    defaultConfig {
        applicationId "com.jenkins"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    // 配置签名
    signingConfigs {
        release {
            storeFile file('cube.keystore')
            storePassword "QWE123"
            keyAlias "cube"
            keyPassword "QWE123"
        }
    }

    // 获取外部传入到channel，来打不同渠道打apk
    def channel = hasProperty('channel') ? channel : "default"
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFile 'proguard-rules.pro'
            manifestPlaceholders = [ CHANNEL_VALUE:channel]
        }
    }

    // 读取channel 信息，以此打包替换channel值
    def flavors = file("channel.txt")
    productFlavors {
        flavors.eachLine { line ->
            line = line.trim();
            if(!line.equals("")){
                "$line" {}
            }
        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [ CHANNEL_VALUE:name ]
    }
}

// 把待发布的包copy到指定目录下
assembleRelease.doLast {

    copy {
        from('build/outputs/apk')
        into('build/output_release_apks')
        exclude '**/*-unaligned.apk'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.networkbench:tingyun-ea-agent-android:2.2.4.0'
}
